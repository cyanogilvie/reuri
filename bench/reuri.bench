if {"bench" ni [info commands bench]} {
	package require bench
	namespace import bench::*
}

package require reuri
package require uri

uri::setQuirkOption NoInitialSlash false

proc main {} {
	bench reuri-1.1 {Parse a uri} -batch auto -setup { #<<<
		set uri_a http://foo:bar@localhost:81
		set uri_b /one/two?three=3&four=4#endbit
		set uri		$uri_a$uri_b
		reuri::uri get $uri scheme
	} -overhead {
		reuri_parse	- uri	{return -level 0 $uri_a$uri_b}
		uri_reuse			{return -level 0 $uri}
	} -compare {
		reuri_parse {
			reuri::uri get $uri_a$uri_b fragment
		}
		reuri_reuse {
			reuri::uri get $uri fragment
		}
		uri {
			dict get [uri::split $uri_a$uri_b] fragment
		}
	} -cleanup {
		unset -nocomplain uri_a uri_b uri
	} -result endbit
	#>>>
	bench reuri-2.1 {Parse a uri} -batch auto -setup { #<<<
		set uri_a http://foo:bar@localhost:81
		set uri_b /one/two?three=3&four=4#endbit
		set uri		$uri_a$uri_b
		reuri::uri get $uri scheme
	} -overhead {
		reuri_reparse				{set newuri $uri_a$uri_b; return -level 0 $newuri}
		reuri_reuse					{return -level 0 $uri}
		reuri_dict_reparse - uri	{return -level 0 $uri_a$uri_b}
		reuri_dict_reuse			{return -level 0 $uri}
	} -compare {
		reuri_reparse {
			set newuri	$uri_a$uri_b
			list \
				[reuri::uri extract $newuri scheme] \
				[reuri::uri extract $newuri host] \
				[reuri::uri extract $newuri port 80] \
				[reuri::uri extract $newuri path]
		}
		reuri_reuse {
			list \
				[reuri::uri extract $uri scheme] \
				[reuri::uri extract $uri host] \
				[reuri::uri extract $uri port 80] \
				[reuri::uri extract $uri path]
		}
		reuri_dict_reparse {
			set parts	[reuri::uri extract $uri_a$uri_b]
			list \
				[dict get $parts scheme] \
				[dict get $parts host] \
				[dict get $parts port] \
				[dict get $parts path]
		}
		reuri_dict_reuse {
			set parts	[reuri::uri extract $uri]
			list \
				[dict get $parts scheme] \
				[dict get $parts host] \
				[dict get $parts port] \
				[dict get $parts path]
		}
		uri {
			set parts	[uri::split $uri_a$uri_b]
			list \
				[dict get $parts scheme] \
				[dict get $parts host] \
				[dict get $parts port] \
				[dict get $parts path]
		}
	} -cleanup {
		unset -nocomplain uri_a uri_b newuri uri parts
	} -result {http localhost 81 /one/two}
	#>>>
	bench reuri_valid-2.1 {Validate a uri, valid} -batch auto -setup { #<<<
		set uri_a http://foo:bar@localhost:81
		set uri_b /one/two?three=3&four=4#endbit
	} -overhead {
		* {reuri::nop $uri_a$uri_b}
	} -compare {
		reuri	{reuri::uri valid $uri_a$uri_b}
		uri		{expr {![catch {uri::split $uri_a$uri_b}]}}
	} -cleanup {
		unset -nocomplain uri_a uri_b newuri
	} -result 1
	#>>>
	bench reuri_valid-2.2 {Validate a uri, invalid} -batch auto -setup { #<<<
		set uri_a http://fo\u306fo:bar@localhost:81
		set uri_b /one/two?three=3&four=4#endbit
	} -overhead {
		* {reuri::nop $uri_a$uri_b}
	} -compare {
		reuri	{reuri::uri valid $uri_a$uri_b}
		uri		{expr {![catch {uri::split $uri_a$uri_b}]}}
	} -cleanup {
		unset -nocomplain uri_a uri_b newuri
	} -result 0
	#>>>
}

main

# vim: ft=tcl foldmethod=marker foldmarker=<<<,>>> ts=4 shiftwidth=4
